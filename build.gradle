plugins {
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'maven-publish'
    id 'java'
    id 'java-gradle-plugin'
    id 'eclipse'
    id 'net.minecraftforge.gradleutils' version '2.2.0'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

repositories {
    mavenCentral()
}

group = 'net.minecraftforge'
java.toolchain.languageVersion = JavaLanguageVersion.of(8)
java.withSourcesJar()

license {
    header project.file('LICENSE-header.txt')
    newLine false
}

version = gradleutils.tagOffsetVersion
println('Version: ' + version)

gradlePlugin {
    website = 'https://github.com/MinecraftForge/GradleJarSigner'
    vcsUrl = 'https://github.com/MinecraftForge/GradleJarSigner.git'
    plugins {
        gradlejarsigner {
            id = 'net.minecraftforge.gradlejarsigner'
            implementationClass = 'net.minecraftforge.gradlejarsigner.GradleJarSignerPlugin'
            displayName = 'Gradle Jar Signer'
            description = 'Wrapper for Ant signjar, allowing proper task caching'
            tags.set(['signing', 'java', 'signjar'])
        }
    }
}

dependencies {
}

compileJava {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes([
            'Specification-Title':   'GradleJarSigner',
            'Specification-Vendor':  'Forge Development LLC',
            'Specification-Version':  gradleutils.gitInfo.tag,
            'Implementation-Title':  'GradleJarSigner',
            'Implementation-Vendor': 'Forge Development LLC',
            'Implementation-Version': project.version
        ] as LinkedHashMap, 'net/minecraftforge/gradlejarsigner/')
    }
}

changelog {
    fromTag '1.0'
    publishAll = false
}

publishing {
    publications.register('pluginMaven', MavenPublication) {
        changelog.publish(it)
        pom {
            packaging = 'jar'
            description = 'Gradle Jar Signer Plugin'
            url = 'https://github.com/MinecraftForge/GradleJarSigner'

            gradleutils.pom.setGitHubDetails(pom, 'GradleJarSigner')

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}
